apply plugin: 'com.android.application'

android {
    compileSdk 34
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId "tkaxv7s.xposed.sesame"
        minSdk 21
        targetSdk 31

        // 版本号和版本名的处理
        def version = project.hasProperty("version") ? project.version : "TEST"
        versionCode 80
        versionName version
    }

    flavorDimensions.add("default")
    productFlavors {
        normal {
            dimension "default"
            ext.applicationType = "Normal"
        }
        compatible {
            dimension "default"
            ext.applicationType = "Compatible"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // 自定义输出文件名
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                        def apkName = "${applicationName}-${variant.productFlavors[0].ext.applicationType}-${defaultConfig.versionName}.apk"
                        output.outputFileName = apkName
                    }
                }
            }
        }
    }
}

dependencies {
    // 依赖配置
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    implementation 'com.elvishew:xlog:1.11.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // 根据 productFlavors 配置不同的 Jackson 版本
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.17.1'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.1'
    normalImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.17.1'

    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.5'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.5'
    compatibleImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.5'
}
